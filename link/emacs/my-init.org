#+TITLE:   Emacs Initialization File
#+AUTHOR:  Thomas Trabue
#+EMAIL:   tom.trabue@gmail.com
#+DATE:    2020:5:25
#+TAGS:    emacs initialization init
#+STARTUP: fold

This Org document contains my primary Emacs configuration. =~/.emacs= only holds
pieces of code necessary to prepare and load this file. You can find my full
dotfiles repository [[https://github.com/tjtrabue/dotfiles][here]].

* How to Load the Complete Configuration
I split my Emacs configuration across many files, most of which reside in
literate Org configuration blocks in =~/.emacs.d/plugin-notebook/=. Tangling and
loading all of those files using Org Babel can greatly add to Emacs' initial
load time. To reduce startup time, I created an executable script called
=make_emacs_super_config= that tangles all of my Org configuration files and
concatenates them into a series of Emacs Lisp files under
=~/.emacs.d/super_config=. My =~/.emacs= file knows to look for the super config
directory, and, if it does not yet exist, generate it using the aforementioned
script.

Thus, before starting Emacs, you should run this command to generate the super
config:

#+begin_src sh :tangle no
  ~/.dotfiles/bin/make_emacs_super_config
#+end_src

Now, start Emacs, sit back, and enjoy!

* Known Issues (PLEASE READ)
Here I list any known issues with my configuration and/or plugins, and any
workarounds that you should use temporarily while the issues are being resolved.
If there are no subheadings below this one, you may assume there are no known
issues and proceed without fear. I update this section regularly, so please
check it frequently for important updates you must make to your configuration.

* NOTE: Slow Emacs Initial Load
When running a fresh Emacs installation with all the goodies for the first time
(things like AOT native compilation, jitted trampolines, etc.), Emacs will
probably take a long time to fully load due to the insane number of Elisp files
Emacs must compile to native code. I'm talking many hours, possibly a full day
depending on the beefiness of your CPU. This can be discouraging, but it is
nothing to worry about. Emacs will eventually finish compiling all your
files...usually. If Emacs does hang during compilation, for some reason, it can
be difficult to tell if Emacs is still running or if something went wrong. Here
are some tips for ensuring Emacs does, in fact, finish compiling your Elisp
files and become useable:

- Make sure to set the max number of open file descriptors in
  =/etc/security/limits.conf= to something pretty high:

  #+begin_quote
  * soft nofile 8192
  * hard nofile 65535
  #+end_quote

- Run Emacs in daemon-mode from the command line to keep track of the native
  compilation process:

  #+begin_src shell :tangle no
    # First, set the max number of open file descriptors to a sufficiently high
    # value.
    ulimit -Sn 8192
    ulimit -Hn 65535
    # Then, run Emacs in server-mode and keep it in the terminal's foreground to
    # keep track of Emacs' native compilation process. Once complete, Emacs will
    # print "Starting Emacs daemon."
    emacs --fg-daemon
  #+end_src

* Define User Variables
This is where I define new variables needed throughout my configuration.

#+begin_src emacs-lisp
  ;; Make these variables compile-time constants (and available during
  ;; compilation)
  (eval-and-compile
    (defconst my/use-straight-p t
      "Whether to use straight.el instead of Emacs' built-in package manager.")
    (defconst my/emacs-plugin-dir (file-truename
                                   (concat user-emacs-directory "/plugin"))
      "Directory for additional elisp config files.
  Mostly for enumerating and configuring downloaded plugins.")
    (defconst my/plugin-notebook-dir (file-truename
                                      (concat user-emacs-directory
                                              "/plugin-notebook"))
      "Directory for config files written in Org syntax.")
    (defconst my/elisp-lib-dir (file-truename
                                (concat user-emacs-directory "/require"))
      "Directory for custom Emacs Lisp library files.")
    (defconst my/private-elisp-dir (file-truename
                                    (concat user-emacs-directory "/private"))
      "Directory containing private config for my local environment.")
    (defconst my/local-repos-dir (file-truename
                                  (concat user-emacs-directory "/local-repos"))
      "Directory for local `straight' repos.")
    (defconst my/emacs-backup-dir (file-truename
                                   (concat user-emacs-directory "/backups"))
      "Directory housing all Emacs backup files.")
    (defconst my/emacs-auto-saves-dir (file-truename
                                       (concat user-emacs-directory "/auto-saves"))
      "Directory housing all Emacs auto-save files.")
    (defconst my/emacs-lisp-dir (file-truename
                                 (concat user-emacs-directory "/lisp"))
      "Extra miscellaneous elisp code goes here (if necessary).")
    (defconst user-fonts-dir (file-truename
                              (concat (getenv "HOME") "/.local/share/fonts"))
      "Fonts directory for the current user")
    (defconst my/static-path-file (file-truename
                                   (concat (getenv "HOME") "/.path_static"))
      "The compiled PATH file used to quickly determine PATH.")
    (defconst my/straight-home-dir (file-truename
                                    (concat user-emacs-directory "/straight"))
      "Home directory for the straight package manager.")
    (defconst my/straight-repos-dir (file-truename
                                     (concat my/straight-home-dir "/repos"))
      "Housing area for repositories cloned by straight.")
    (defconst my/straight-build-dir (file-truename
                                     (concat my/straight-home-dir "/build"))
      "Housing area for linked and compiled Elisp files for packages installed
  with straight.el.")
    (defconst my/workspace-dir (file-truename
                                (concat (getenv "HOME") "/workspace"))
      "Directory containig development projects.")
    (defconst my/practice-dir (file-truename
                               (concat (getenv "HOME") "/practice"))
      "Directory containig practice or scratch code.")
    (defconst my/line-width 80 "The width of each line of code.")
    (defconst my/use-helm-gtags t
      "If non-nil, use helm-gtags over the ggtags package. Otherwise, use ggtags.")
    (defconst my/user-info-dir
      (file-truename (concat (getenv "HOME") "/.local/share/info"))
      "The directory containing extra info pages installed by the user.")
    (defconst my/max-worker-processes
      (string-trim (shell-command-to-string "nproc"))
      "The maximum number of processor threads that can be running at once.")
    (defconst my/org-dir
      (file-truename (concat user-emacs-directory "/org"))
      "My custom directory used by Org to store miscellaneous notes.")
    (defconst my/org-agenda-dir (file-truename (concat my/org-dir "/agenda"))
      "Directory containing my Org agenda files.")
    (defconst my/org-capture-file (file-truename (concat my/org-dir "/inbox.org"))
      "File receiving Org captures from other files. A dumping ground for ideas.")
    (defconst my/lisp-major-modes
      '(cider-repl-mode
        clojure-mode
        clojurec-mode
        clojurescript-mode
        common-lisp-mode
        emacs-lisp-mode
        ielm-mode
        lisp-mode
        lisp-data-mode
        lisp-interaction-mode
        sly-mrepl-mode)
      "List of important Lisp major modes")
    (defvar my/authinfo-files (mapcar (lambda (file)
                                        (file-truename (concat (getenv "HOME") "/" file)))
                                      '(".authinfo.gpg" ".authinfo" ".netrc"))
      "The user's .authinfo files used to store login credentials.")
    (defconst my/line-number-state 'absolute
      "The current global line number setting. Either `absolute' or `relative'.")
    (defconst my/super-config-dir (file-truename (concat user-emacs-directory "super_config"))
      "The path to the directory containing my loadable configuration files."))
#+end_src

* Create Directories
Create directories that must be present for our Emacs configuration to function.

#+begin_src emacs-lisp
  (make-directory my/emacs-plugin-dir 'and-parents)
#+end_src

* Language/Encoding
Language and encoding parameters for this particular Emacs instance.

#+begin_src emacs-lisp
  ;; Define a standard text encoding system for all files.
  (set-language-environment "UTF-8")
  (prefer-coding-system 'utf-8)
  (set-default-coding-systems 'utf-8)
  (set-terminal-coding-system 'utf-8)
  (set-keyboard-coding-system 'utf-8)
#+end_src

* Load Hotfix Modules
Load any HOTFIX modules in order to fix backwards-compatibility issues.  It is
best to keep these code snippets out of the main Emacs configuration file since
they are necessary evil, not main configuration code, and as such will soon
become unnecessary and may be safely removed.

If no code block follows this paragraph, then my current configuration requires
no hot-fixes (in which case there will be much rejoicing).

* Configure Backup Dirs/Auto-Saves
It's handy to have Emacs put all backup files in a centralized directory, as
opposed to strewing them about each directory you visit. Same goes for the
auto-save feature for buffers.

#+begin_src emacs-lisp
  ;; Put all backup files in ~/.emacs.d/backups and auto save files in
  ;; ~/.emacs.d/auto-saves to avoid causing unwanted side-effects.
  (dolist (dir (list my/emacs-backup-dir my/emacs-auto-saves-dir))
    (when (not (file-directory-p dir))
      (make-directory dir t)))
  (setq backup-directory-alist
        `((".*" . ,(file-truename (concat my/emacs-backup-dir "/")))))
  (setq auto-save-file-name-transforms
        `((".*" ,(file-truename (concat my/emacs-auto-saves-dir "/")) t)))
  (setq auto-save-list-file-prefix
        (file-truename (concat my/emacs-auto-saves-dir "/.saves-")))
  (setq tramp-backup-directory-alist
        `((".*" . ,(file-truename my/emacs-backup-dir))))
  (setq tramp-auto-save-directory
        (file-truename (concat my/emacs-auto-saves-dir "/")))

  ;; Backup of a file the first time it is saved.
  (setq make-backup-files t)
  ;; Don't clobber symlinks
  (setq backup-by-copying t)
  ;; Version numbers for backup files
  (setq version-control t)
  ;; Delete excess backup files silently
  (setq delete-old-versions t)
  (setq delete-by-moving-to-trash nil)
  ;; Oldest versions to keep when new numbered backups created (default 2)
  (setq kept-old-versions 2)
  ;; Newest versions to keep when new numbered backups created (default 2)
  (setq kept-new-versions 5)
  ;; Auto-save every buffer that visits a file
  (setq auto-save-default t)
  ;; Number of seconds idle time before auto-save (default 30)
  (setq auto-save-timeout 30)
  ;; Number of keystrokes between auto-saves (default 300)
  (setq auto-save-interval 300)
#+end_src

* Set Emacs Variables
Here is where we set existing Emacs variables to our preferred values, both for
customization and performance. Emacs is notoriously slow unless you tweak
GC-related variables, especially if you use advanced programming tools such as
LanguageServerProtocol clients and servers.

#+begin_src emacs-lisp
  ;; Always follow symlinks
  (setq vc-follow-symlinks t)
  ;; Reduce risk of loading outdated bytecode
  (setq load-prefer-newer t)

  ;; Do not show standard GNU Emacs welcome screen when Emacs starts,
  ;; but instead enter the *scratch* buffer.
  (setq inhibit-startup-screen t)

  ;; Silence the annoying error/warning bell
  (setq ring-bell-function 'ignore)

  ;; Suppress specified warning types.
  (setq warning-suppress-log-types '((comp)))

  ;; Describe the types of byte-compile warnings disired
  ;; as a list. `nil' means present no byte compile warnings.
  ;; `t' means present nearly all of them. `all' means
  ;; present absolutely all of them.
  (setq byte-compile-warnings t)

  ;; Whether to automatically scroll the compilation window as output arrives.
  (setq compilation-scroll-output t)

  ;; How to display line numbers:
  ;; - 'relative: Relative line numbers
  ;; -         t: Absolute line numbers
  ;; -       nil: No line numbers
  (setq display-line-numbers (pcase my/line-number-state
                               ('relative 'relative)
                               ('absolute t)
                               (_ nil)))
  (setq display-line-numbers-type (pcase my/line-number-state
                                    ('relative 'relative)
                                    ('absolute t)
                                    (_ nil)))

  ;; Up the maximum depth for eval, apply, and funcall functions.  This variable
  ;; catches infinite recursions before they cause a stack overflow, but its
  ;; default value is low.
  (setq max-lisp-eval-depth 10101)

  ;; Get rid of scrollbars since l33t programmers don't need any.
  (setq vertical-scroll-bar nil)

  ;; Increase the amount of bytes Emacs reads per unit time from a given
  ;; process. The initial value is 4KB, far too low for modern day applications.
  (setq read-process-output-max (* 3 (* 1024 1024)))

  ;; Max file size (in bytes) before a confirmation is required of the user before
  ;; opening.
  (setq large-file-warning-threshold 100000000)

  ;; Echo unfinished commands after this many seconds of pause.
  (setq echo-keystrokes 0.1)

  ;; Set the max number of variable bindings allowed at one time to a
  ;; number considerably higher than the default (which is 1600).
  ;; Modern problems require modern solutions!
  (setq max-specpdl-size 12000)

  ;; Each line should be 80 characters wide.
  (setq-default fill-column my/line-width)

  ;; Set vertical ruler in programming modes
  (setq-default
   whitespace-line-column my/line-width
   whitespace-style '(face lines-tail))

  ;; Smooth-scrolling
  (if (>= emacs-major-version 29)
      ;; Use native smooth-scrolling (requires Emacs version >= 29)
      (progn
        (pixel-scroll-precision-mode 1)
        (when (not (memq system-type '(darwin)))
          ;; Drift a little once scrolling stops (does not work on the NS port on macOS).
          (setq pixel-scroll-precision-use-momentum t)))
    ;; Otherwise, simulate smooth-scrolling with basic Emacs settings.
    ;; (also see the sublimity plugin configuration)
    (setq mouse-wheel-scroll-amount '(1 ((shift) . 1)))
    (setq mouse-wheel-progressive-speed nil)
    (setq mouse-wheel-follow-mouse 't))
  (setq scroll-margin 0)
  (setq scroll-step 1)
  (setq scroll-conservatively 10000)
  (setq auto-window-vscroll nil)

  ;; Automatically reload TAGS file without prompting us.
  (setq tags-revert-without-query t)

  ;; Never prompt us to take tags tables with us when moving between
  ;; directories. Always assume "no".
  (setq tags-add-tables nil)

  ;; Try to indent the current line, or complete the thing at point if the code is
  ;; already properly indented.
  (setq tab-always-indent 'complete)

  ;; Use spaces instead of tabs.
  (setq-default indent-tabs-mode nil)
  ;; Indent in increments of 2 spaces.
  (setq-default tab-width 2)

  ;; Show trailing whitespace characters by default.
  (setq-default show-trailing-whitespace t)

  ;; This must be set to nil in order for evil-collection to replace
  ;; evil-integration in all important ways. This variable must be set
  ;; here, NOT in the :config or :init blocks of a use-package expression.
  ;; (otherwise a warning gets printed)
  (setq evil-want-keybinding nil)

  ;; Enable recursive minibuffers
  (setq enable-recursive-minibuffers t)

  ;; Do not allow the cursor in the minibuffer prompt
  (setq minibuffer-prompt-properties
        '(read-only t cursor-intangible t face minibuffer-prompt))

  ;; Move custom set variables to a separate file so as not to clutter my personal
  ;; initialization files.
  (setq custom-file (locate-user-emacs-file "custom-vars.el"))

  ;; Whether to use a graphical dialog box for user input.  Disabling this option
  ;; causes Emacs to prompt the user from the minibuffer instead, keeping Emacs
  ;; more keyboard-centric.
  (setq use-dialog-box nil)

  ;; Automatically revert Dired and other buffers when the filesystem updates.
  (setq global-auto-revert-non-file-buffers t)

  ;; Display the name of the real file when visiting a symbolic link.
  ;; WARNING: DO NOT SET THIS TO T! It messes with straight.el's autoload
  ;; generation!
  (setq find-file-visit-truename nil)

  ;; Controls whether and when Emacs saves bookmarks to disk.
  ;;   nil    -> Emacs never saves bookmarks.
  ;;   t      -> Emacs saves bookmarks when it is killed.
  ;;   NUMBER -> Emacs will save bookmarks to file after NUMBER changes
  ;;             are made to bookmarks (i.e., if NUMBER is 1, Emacs will
  ;;             will save the bookmarks file every time a bookmark is created
  ;;             or deleted).
  (setq bookmark-save-flag 1)

  ;; Don’t compact font caches during GC. This can resolve lag issues with
  ;; doom-modeline and some other plugins.
  (setq inhibit-compacting-font-caches t)

  ;; Whether to cycle completions.
  (setq completion-cycle-threshold t)

  ;; Whether to prompt user for confirmation before killing Emacs when child processes are running.
  (setq confirm-kill-processes nil)

  ;; If non-nil, redraw the display before processing queued input events.
  ;; NOTE: Can make the typing experience slower.
  (setq redisplay-dont-pause t)

  ;; Show file name and major mode in title bar.
  (setq-default frame-title-format
                '("%b [%m]@"
                  (:eval (or (file-remote-p default-directory 'host) system-name))
                  " — Emacs"))

  ;; Emacs 28 variables.
  (when (>= emacs-major-version 28)
    ;; Hide commands in M-x which do not work in the current mode.
    ;; Vertico commands are hidden in normal buffers.
    (setq read-extended-command-predicate #'command-completion-default-include-p)
    ;; Automatically native compile all packages installed with package.el
    (setq package-native-compile t))

  ;; Emacs supports editing text in languages that order text horizontally
  ;; right-to-left, such as Hebrew or Arabic. If you do not work in a language
  ;; such as these, you can improve Emacs' performance if you tell it to assume
  ;; all languages display left-to-right by default, resulting in fewer line scans
  ;; necessary to display text.
  (setq-default bidi-paragraph-direction 'left-to-right)
  (if (version<= "27.1" emacs-version)
      (setq bidi-inhibit-bpa t))

  ;;; EasyPG settings (Emacs' front-end for GPG)
  ;; Whether to cache the user's passphrases for symmetrically encrypted files.
  (setq epa-file-cache-passphrase-for-symmetric-encryption t)
  ;; How to prompt the user for passphrases.
  ;; 'loopback means to query passphrases through the minibuffer.
  (setq epg-pinentry-mode 'loopback)

  ;;; Browser
  ;; Determines the default web browser function to use when opening a URL via
  ;; `browse-url-at-point', `browse-url-at-mouse', and `browse-url-of-file'.
  (setq browse-url-browser-function #'browse-url-generic)
  ;; Set default browser to the first of a ranked list of programs.
  (setq browse-url-generic-program (seq-some #'executable-find
                                             '("brave-beta"
                                               "brave"
                                               "chromium"
                                               "firefox"
                                               "chrome")))

  (when (display-graphic-p)
    ;; How to handle child frames. Can be nil or 'resize-mode.  Setting this
    ;; variable to 'resize-mode may improve the performance of plugins that use
    ;; child frames.
    (setq posframe-gtk-resize-child-frames 'resize-mode))
#+end_src

* Package Manager
Configure package managers Emacs leverages to install and configure third-party
packages.

** Standard Config
Here we determine whether to use Emacs' built-in package manager =package.el= or
the much superior =straight.el= third-party package manager.

#+begin_src emacs-lisp
  (if (and (not my/use-straight-p) (>= emacs-major-version 24))
  ;;; IF we want to use the built-in package manager...
      (progn
        ;; Package configuration
        (require 'package)
        ;; Add extra package archives to the list of repositories.
        ;; NOTE: HTTPS may be unsupported on Emacs versions < 27. You may need
        ;;       to change the URLs to simple HTTP in order for them to function.
        ;;       If you must do this, also uncomment the two expressions below.
        ;;       That will reset the archives list and allow you to only use
        ;;       unsecured connections for package transfer.
        ;; (setq package-archives nil)
        ;; (add-to-list 'package-archives
        ;;   '("gnu" . "http://elpa.gnu.org/packages/") t)
        (add-to-list 'package-archives '("org"       . "https://orgmode.org/elpa/") t)
        (add-to-list 'package-archives '("melpa"     . "https://melpa.org/packages/") t)
        (add-to-list 'package-archives '("marmalade" . "https://marmalade-repo.org/packages/") t)
        (package-initialize)
        ;; Automatically install packages using use-package
        (unless (package-installed-p 'use-package)
          (package-refresh-contents)
          (package-install 'use-package))
        (require 'use-package))
  ;;; OTHERWISE...
    ;; Do not auto-initialize packages! This can slow down Emacs's startup time.
    (setq package-enable-at-startup nil)
    ;; this tells package.el not to add those pesky customized variable settings
    ;; at the end of your init.el
    (setq package--init-file-ensured t))
#+end_src

** straight
=straight= is a newer package manager for Emacs that differs from
=package.el=.  It operates by cloning Git repositories for Emacs packages and
sym-linking them to Emacs' runtime path. =straight= is also a purely
functional package manager, and integrates nicely with the =use-package=
macro.  *NOTE:* straight requires Emacs version 24.5 or higher to properly
function.

To update all packages installed through straight, run =M-x
straight-pull-all=

#+begin_src emacs-lisp
  (load (file-truename (concat user-emacs-directory "straight/repos/straight.el/bootstrap.el")))
#+end_src

* Load Built-In Libraries
Load built-in Elisp packages used heavily by plugins and my custom code. Please
only list the most important modules here. Note how we can use =straight.el= to
load packages bundled with Emacs just as easily as third-party packages, which
is ideal because =straight.el= handles file compilation and lazy-loading. I pray
Emacs incorporates =straight.el= into the core project someday soon and I get to
remove this wistful aside (and my custom bootstrap code for =straight.el=).

#+begin_src emacs-lisp
  ;; Man pager library
  (use-package man
    :ensure nil
    :straight (man :type built-in)
    :demand t)
  ;; Newer dired library
  (use-package dired-x
    :ensure nil
    :straight (dired-x :type built-in))
  ;; String manipulation library
  (use-package subr-x
    :ensure nil
    :straight (subr-x :type built-in))
  ;; Make C and C++ modes available; necessary for some third-party packages
  (use-package cc-mode
    :ensure nil
    :straight (cc-mode :type built-in))
  ;; "find function at point" library
  (use-package ffap
    :ensure nil
    :straight (ffap :type built-in))
  ;; EasyPG, Emacs' front-end for GPG.
  (use-package epa
    :ensure nil
    :straight (epa :type built-in))
  ;; Emacs Grep integration functions/configuration.
  (use-package grep
    :ensure nil
    :straight (grep :type built-in)
    :commands (grep-apply-setting))
#+end_src

* Load Custom Libraries
Once we have configured the =load-path=, we can load custom Emacs Lisp
libraries. Using =straight.el= as the module loader takes care of lazy loading.

#+begin_src emacs-lisp
  ;; My own font functions and definitions.
  (use-package my-font
     :ensure nil
     :straight
     (my-font :local-repo "~/.emacs.d/require"
              :files ("my-font.el")))

  ;; My custom window functions.
  (use-package my-window
    :ensure nil
    :straight
    (my-window :local-repo "~/.emacs.d/require"
               :files ("my-window.el")))

  ;; Functions for interacting with hooks.
  (use-package my-hook-fns
    :ensure nil
    :straight
    (my-hook-fns :local-repo "~/.emacs.d/require"
                 :files ("my-hook-fns.el")))

  ;; recentf library for managing recently accessed filed.
  (use-package my-recentf
    :ensure nil
    :straight
    (my-recentf :local-repo "~/.emacs.d/require"
                :files ("my-recentf.el")))

  ;; GTAGS function library.
  (use-package my-gtags
    :ensure nil
    :straight
    (my-gtags :local-repo "~/.emacs.d/require"
              :files ("my-gtags.el")))

  ;; straight.el convenience functions.
  (use-package my-straight-helpers
    :ensure nil
    :straight
    (my-straight-helpers :local-repo "~/.emacs.d/require"
                         :files ("my-straight-helpers.el")))

  ;; Custom completing-read functions.
  (use-package my-completing-read
    :ensure nil
    :straight
    (my-completing-read :local-repo "~/.emacs.d/require"
                        :files ("my-completing-read.el")))

  ;; My customized buffer tab-line, based on Emacs' built-in version.
  (use-package my-tab-line
    :ensure nil
    :straight
    (my-tab-line :local-repo "~/.emacs.d/require"
                 :files ("my-tab-line.el"))
    :init
    (my-tab-line-mode 1))

  ;; Custom functions for making/starting processes.
  (use-package my-process
    :ensure nil
    :straight
    (my-process :local-repo "~/.emacs.d/require"
                :files ("my-process.el")))
#+end_src

* Color Configuration
Here we configure ANSI colors for major and minor modes used throughout Emacs.
We need to make =comint-mode=, which all shell emulators in Emacs (known as
inferior interpreters) inherit from, recognize ANSI color escape sequences so
that shells don't look like a hot mess. We also want colorized man pages.

#+begin_src emacs-lisp
  ;; Set ANSI color integration in comint-mode
  (add-to-list 'comint-output-filter-functions 'ansi-color-process-output)
  ;; Colorize Emacs' man page viewer
  (set-face-attribute 'Man-overstrike nil
                      :inherit font-lock-type-face
                      :bold t)
  (set-face-attribute 'Man-underline nil
                      :inherit font-lock-keyword-face
                      :underline t)
#+end_src

* Grep/Search
Being one of the flagship GNU programs, Emacs has wondrous integration with
other GNU tools, not the least of which is Grep. Emacs implements a number of
interactive, templated Grep commands:

- =lgrep=: Limited grep search for PATTERN in FILES starting in DIR.
- =rgrep=: Recursive, total grep search for PATTERN in FILES starting in DIR.
- =rzgrep=: Recursive, total grep search for PATTERN in gzipped FILES starting
  DIR.
- =grep-find=: Run grep via find.

#+begin_src emacs-lisp
  (let ((has-ugrep (executable-find "ugrep"))
        (has-rg    (executable-find "rg"))
        (find-cmd-str "find -H <D> <X> -type f <F> -exec "))
    ;; Change standard `grep-find' command to use Ripgrep when available.  The numbers means place the
    ;; cursor at that character position, right between the '' where we will begin typing our search
    ;; expression.
    (grep-apply-setting 'grep-find-command
                        (cond (has-ugrep
                               `(,(concat "ugrep -e '' --index --no-heading -HI0inr"
                                          " --ignore-files=\"$(git rev-parse --show-toplevel)/.gitignore\""
                                          " -- \"$(git rev-parse --show-toplevel || pwd)\"")
                                 . 11))
                              (has-rg
                               '("rg -e '' -n -H --no-heading $(git rev-parse --show-toplevel || pwd)"
                                 . 8))
                              (t nil)))

    ;; The default command to run for `lgrep'.
    (grep-apply-setting 'grep-template
                        (cond (has-ugrep
                               "ugrep <C> <X> --color=always --index --no-heading -HIPine <R> <F>")
                              (has-rg
                               "rg --color=always --no-heading -HPine <R> <F>")
                              (t nil)))
    ;; The default command to use for `rgrep'.
    (grep-apply-setting 'grep-find-template
                        (cond (has-ugrep
                               (concat find-cmd-str
                                       "ugrep <C> --color=always --index --no-heading -0HIPine <R> \\{\\} +"))
                              (has-rg
                               (concat find-cmd-str
                                       "rg --color=always --no-heading -0HPne <R> \\{\\} +"))
                              (t
                               (concat find-cmd-str "grep <C> -nH --null -e <R> \\{\\} +")))))
#+end_src

* Set fringe width
In Emacs, the /fringe/ is the margin on the left and/or right side of a frame
between the edge of the frame and the first buffer. You can even set the width
of the right and left fringes individually.

#+begin_src emacs-lisp
  (when (display-graphic-p)
    ;; When called with a number, set the fringe on the right and left to the
    ;; specified number of pixels.  When called interactively, prompt the user for a
    ;; fringe style to apply.
    (set-fringe-mode 8))
#+end_src

* Adjust initial frame size
In keeping with the spirit of Emacs, there are a plethora of methods for
changing the size of the first frame Emacs creates. A frame is basically Emacs'
concept of a window in Microsoft Windows or macOS lingo. The method(s) used
below are the most portable.

** Fullscreen options
To change the initial fullscreen behavior of a frame using =initial-frame-alist=
or =default-frame-alist=, append one of the following options to one or both of
those lists:

- ='(fullscreen . fullwidth)=: Make the frame as wide as possible, but do not
  adjust vertical size.
- ='(fullscreen . fullheight)=: Make the frame as tall as possible, but do not
  adjust horizontal size.
- ='(fullscreen . fullboth)=: Set height and width to the size of the screen.
- ='(fullscreen . maximized)=: Like =fullboth=, but you cannot readjust the
  frame size later with the mouse.

** How to adjust the initial frame's size
Use the =initial-frame-alist= to change the size of the first frame Emacs
creates on startup.

** How to adjust all frames' sizes
To change the size of all frames Emacs creates, use =default-frame-alist=.

** Initial frame size
#+begin_src emacs-lisp
  (when (eq system-type 'darwin)
    ;; Maximize Emacs' initial frame on macOS.
    (add-to-list 'initial-frame-alist `(fullscreen . fullboth)))
#+end_src

* Font Configuration
Set default font for Emacs.

*NOTE:* The main font configuration is in =my-font.el=.

#+begin_src emacs-lisp
  (my-font-set-default-font)
#+end_src

* Info
=info= is Emacs' built in help system. You use =info= to browse documentation
pages. However, by default, Emacs only looks in a small number of locations for
help pages. Here we add more locations for browsing user-installed info pages.

#+begin_src emacs-lisp
  ;; Make sure user-installed info pages are available.
  (add-to-list 'Info-default-directory-list my/user-info-dir)
#+end_src

* Aliases
Here we alias existing functions to new names, usually to tell Emacs to run a
different function whenever it tries to use one we don't like.

** Change "yes or no" to "y or n"
Turn all "yes or no" prompts into "y or n" single character prompts to make
our lives easier.

#+begin_src emacs-lisp
  (defalias 'yes-or-no-p 'y-or-n-p)
#+end_src

* Activate/Deactivate Default Minor Modes
Turn certain minor modes on or off by default. You can think of a minor mode as
a plugin, or an extra set of functions and behaviors that the user turns on or
off by calling the minor mode's function. For instance, calling
=(save-place-mode 1)= will make Emacs open previously closed files at their last
edited location, as opposed to opening them at the beginning of the file.

#+begin_src emacs-lisp
  ;; Disable menubar and toolbar (they take up a lot of space!)
  (menu-bar-mode -1)
  (tool-bar-mode -1)
  ;; Also diable the scrollbar
  (toggle-scroll-bar -1)

  ;; Open files at last edited position
  (save-place-mode 1)

  ;; Use recentf: bind to a keybinding, save recentf list to filesystem every so
  ;; often.
  (my-recentf-enable)

  ;; subword-mode is super handy! It treats parts of camelCase and snake_case
  ;; names as separate words. This enables subword-mode in all buffers.
  (global-subword-mode 1)

  ;; Automatically insert closing delimiters when the user types an opening
  ;; delimiter.
  (electric-pair-mode 1)

  ;; Automatically keep code indented when blocks change.
  (electric-indent-mode 1)

  ;; Allow tooltips in pop-up mini-frames.
  (tooltip-mode 1)

  ;; Turn on syntax highlighting (AKA font locking) by default.
  (global-font-lock-mode 1)

  ;; Always show line numbers
  (global-display-line-numbers-mode 1)

  ;; Keep buffers in sync with their respective files on disk as those files
  ;; change outside of Emacs. An example would be the user adding a previously
  ;; untracked file to the Git index. With this mode active, Emacs will update Git
  ;; information automatically upon adding the file. If this mode is not active,
  ;; the user will have to manually revert the buffer to see the updated
  ;; information.
  ;;
  ;; NOTE: Enabling global-auto-revert can cause Emacs to slow down!
  (global-auto-revert-mode 1)

  ;; Persist command history to disk to maintain it between restarts.
  (savehist-mode 1)

  ;; Automatically visit image files as images.
  (auto-image-file-mode 1)

  ;; Display file size in mode line.
  (size-indication-mode 1)

  ;; Turns on column numbers in mode line.
  (column-number-mode 1)

  ;; Automatically uncompress files when you visit them, and recompress them if
  ;; you alter and save them.  This mode is necssary when your Elisp files are
  ;; compressed as `.el.gz' files, which is often the default for Elisp bundled
  ;; with Emacs.
  (auto-compression-mode 1)

  ;; Highlight the current line based on a customizable face.
  (global-hl-line-mode 1)

  ;; Activate `visual-line-mode' in all buffers.
  ;;
  ;; `visual-line-mode' wraps text at word boundaries at the window's edge so that you do not have to
  ;; horizontally scroll to read long lines.
  (global-visual-line-mode 1)
#+end_src

* Key Bindings
Custom key bindings.

** Global
Key bindings available in any major mode.

#+begin_src emacs-lisp
  ;; Indent according to major mode after pressing Enter.
  (global-set-key (kbd "RET") #'newline-and-indent)

  ;; Change window size (Vim-like bindings)
  (global-set-key (kbd "S-C-l") #'enlarge-window-horizontally)
  (global-set-key (kbd "S-C-h") #'shrink-window-horizontally)
  (global-set-key (kbd "S-C-j") #'enlarge-window)
  (global-set-key (kbd "S-C-k") #'shrink-window)

  ;; Turns vertically split frame into a horizontal split one.
  (global-set-key (kbd "C-c w t") #'my-window-toggle-frame-split)

  ;; Select a bookmark to delete by means of an interactive menu.
  (global-set-key (kbd "C-c D") #'bookmark-delete)
#+end_src

* Email
Settings for Emacs' =mail-mode= and integration with external email programs,
such as =mutt= and =mu=.

#+begin_src emacs-lisp
  ;; Change mode when editing emails for Mutt
  (setq auto-mode-alist (append '(("/tmp/mutt.*" . message-mode)) auto-mode-alist))
#+end_src

* Function Definitions
Custom functions, both standard and interactive.

#+begin_src emacs-lisp
  (unless (fboundp 'my/apply-to-dir-files)
    ;; May need to define this function here since it's originally defined in `~/.emacs', which
    ;; isn't always loaded (i.e., it's only loaded for interactive Emacs sessions).
    (defsubst my/apply-to-dir-files (dir pattern fn &rest args)
      "Apply FN to all files in DIR matching regex PATTERN.

  Any additional args ARGS are passed to FN."
      (cl-flet
          ((apply-it (f) (funcall fn (concat (file-name-as-directory dir) f) args)))
        (if (file-directory-p dir)
            (mapc #'apply-it (directory-files dir nil pattern))))))

  (defun print-major-mode ()
    "Show the major mode of the current buffer in the echo area."
    (interactive)
    (message "%s" major-mode))

  (defun gnus-new-frame ()
    "Create a new frame and start the Gnus news reader in it."
    (interactive)
    (with-selected-frame (make-frame)
      (gnus)))

  (defun reload-config ()
    "Reload all Emacs config files."
    (interactive)
    (load-file user-init-file))

  (defun download-elisp-lib (url &optional file-name)
    "Downloads an elisp file from a URL to `my/emacs-lisp-dir'.

      If FILE-NAME is omitted or nil, it defaults to the last segment of the URL."
    (if (not file-name)
        (setq file-name (url-file-nondirectory (url-unhex-string url))))
    (let ((file-path (concat my/emacs-lisp-dir (concat "/" file-name))))
      (make-directory my/emacs-lisp-dir t)
      (url-copy-file url (file-truename file-path) t)))

  (defun my/recursive-add-dirs-to-load-path (base-dir &optional subdirs)
    "Recursively add directories from a BASE-DIR to load-path.

    Optionally, SUBDIRS is a list of subdirectory strings beneath BASE-DIR that
    should be added to load-path. If this argument is absent, all subdirectories
    of BASE-DIR are added to load-path."
    (interactive)
    (let ((default-directory base-dir))
      (setq load-path
            (append
             (let ((load-path (copy-sequence load-path))) ; Shadow
               (if subdirs
                   ;; If user supplied list of subdirs, pass it here
                   (normal-top-level-add-to-load-path subdirs)
                 ;; Otherwise, add all directories under base-dir
                 (normal-top-level-add-subdirs-to-load-path)))
             load-path))))

  (defun my/use-mu4e-p ()
    "Return T if the system is configured for `mu4e'. Return NIL otherwise."
    (and (executable-find "mu") (executable-find "mbsync")))

  (defun my/toggle-line-number-type ()
    "Toggle absolute/relative line numbers in all open buffers."
    (interactive)
    ;; Figure out global line number state for all buffer.
    (if (eq my/line-number-state 'absolute)
        (setq my/line-number-state 'relative
              display-line-numbers-type 'relative)
      (setq my/line-number-state 'absolute
            display-line-numbers-type t))
    ;; Apply new line number type to all open buffers.
    (dolist (buffer (buffer-list))
      (with-current-buffer buffer
        ;; Only operate on buffers that display line numbers..
        (when (bound-and-true-p display-line-numbers-mode)
          (if (eq my/line-number-state 'relative)
              (setq display-line-numbers 'relative)
            (setq display-line-numbers t))))))

  (defun my/eval-and-replace ()
    "Replace the preceding sexp with its value."
    (let ((value (eval (preceding-sexp))))
      (backward-kill-sexp)
      (insert (format "%S" value))))

  (defun my/running-wsl-p ()
    "Return non-nil if Emacs is running on Windows Subsystem for Linux."
    (let ((case-fold-search t))
      (or (file-exists-p "/proc/sys/fs/binfmt_misc/WSLInterop")
          (string-match "\\(microsoft\\|WSL\\)"
                        (shell-command-to-string "uname -r | tr -d \"\n\"")))))

  (defun my/reload-dir-locals-for-current-buffer ()
    "Reload vars in .dir-locals.el file for current buffer."
    (interactive)
    (let ((enable-local-variables :all))
      (hack-dir-local-variables-non-file-buffer)))

  (defun compose (&rest fns)
    "Compose FNS together: first to last."
    (cl-destructuring-bind (fun . rest) (reverse fns)
      (lambda (&rest args)
        (seq-reduce (lambda (v f) (funcall f v))
                    rest
                    (apply fun args)))))

  (defun my/load-system-path ()
    "Load $PATH env var from the compiled system path file.

  This function uses the system path in the file designated by the
  variable `my/static-path-file' to set both Emacs' PATH environment
  variable, which inferior shell processes use, as well as the variable
  `exec-path', which is more like a PATH variable for Emacs itself -- it
  is the list of directories Emacs searches when looking for executable
  programs to run."
    (interactive)
    (if (file-exists-p my/static-path-file)
        (let ((system-path (substitute-env-vars
                            (shell-command-to-string
                             (concat "cat " my/static-path-file " | "
                                     "grep '^\\s*PATH=' | "
                                     "sed -e 's/^\\s*PATH=//' -e 's/\"//g' | "
                                     "tr -d '\n'")))))
          ;; Set PATH environment variable, which is used by Emacs shell processes.
          (setenv "PATH" system-path)
          ;; Set `exec-path', which is the variable Emacs uses to search for executable programs.
          (dolist (path (seq-reverse (split-string system-path ":")))
            (add-to-list 'exec-path path)))
      (error (concat "No static path file at " my/static-path-file))))

  (defun my/native-compile-config ()
    "Native compile all Elisp files in the super config directory."
    (interactive)
    (native-compile-async (list my/super-config-dir) 'recursively))
#+end_src

* Environment Variables
Set additional environment variables not taken care of through the
=initial-environment= list of variables.

** Standard
Set standard environment variables that affect Emacs as a whole.

#+begin_src emacs-lisp
  ;; Set standard language that Emacs assumes.
  (setenv "LANG" "en_US.UTF-8")
#+end_src

** Perl
Perl's operations depends on a number of environment variables that Emacs
will not recognize by default, so we must set them here.

#+begin_src emacs-lisp
  (let* ((perl-local-lib-root (concat (getenv "HOME") "/perl5"))
         (perl-local-lib (concat perl-local-lib-root "/lib/perl5")))
    (setenv "PERL5LIB" perl-local-lib)
    (setenv "PERL_LOCAL_LIB_ROOT"
            (concat perl-local-lib-root ":$PERL_LOCAL_LIB_ROOT") 'subst-env-vars)
    (setenv "PERL_MB_OPT" (concat "--install_base '" perl-local-lib-root "'"))
    (setenv "PERL_MM_OPT" (concat "INSTALL_BASE=" perl-local-lib-root))
    (setenv "PERL_MM_USE_DEFAULT" "1"))
#+end_src

** LSP
Set variables used by LSP servers.

#+begin_src emacs-lisp
  ;; lsp-mode can be compiled in two modes: `plist' and `hash-table'.
  ;; Plists provide better performance in deserialization and are lighter than
  ;; hash tables.
  ;; NOTE: You MUST rebuilt all lsp-mode related packages if you change this
  ;;       variable!
  (setenv "LSP_USE_PLISTS" "true")
#+end_src

* Hooks
Hooks are analogous to Vim's =autocmds=. They represent a series of functions to
run when an event occurs. Both Emacs proper and third party plugins expose
certain hooks along with their packages, and the user can then attach functions
to each hook by means of the =add-hook= function. The most commonly used hooks
are those for major and minor modes, each having a name like =java-mode-hook=,
or =company-mode-hook=.  However, most packages provide additional hooks for use
besides those for major and minor modes, such as Evil's state change hooks like
=evil-insert-state-entry-hook= and =evil-insert-state-exit-hook=.

** Buffer-menu-mode hooks
#+begin_src emacs-lisp
  (add-hook 'Buffer-menu-mode-hook (lambda ()
                                     ;; Disable whitespace visualization in Buffer menu.
                                     (setq-local show-trailing-whitespace nil)
                                     (whitespace-mode -1)))
#+end_src

** dired-mode hooks
dired is the awesome "directory editor" mode in Emacs. It's much more
convenient than entering the shell, for the most part.

#+begin_src emacs-lisp
  (add-hook 'dired-mode-hook (lambda ()
                               ;; Auto-refresh dired buffer when files change.
                               (auto-revert-mode 1)
                               ;; Allow user to toggle long-form ls output in dired mode with '('.
                               (dired-hide-details-mode 1)))
  (add-hook 'wdired-mode-hook (lambda ()
                                ;; Auto-refresh wdired buffer when files change.
                                (auto-revert-mode 1)))
#+end_src

** emacs-lisp hooks
Hooks that run upon entering Emacs Lisp source code buffers.

#+begin_src emacs-lisp
  (add-hook 'emacs-lisp-mode-hook
            (lambda ()
              (let ((max-columns 100))
                ;; The Common Lisp style guide recommends 100 columns max instead of 80 due to Lisp
                ;; having longer, more descriptive names.
                (setq-local fill-column max-columns
                            whitespace-line-column max-columns))))
#+end_src

** minibuffer-setup hooks
These hooks just after entry into the minibuffer.

#+begin_src emacs-lisp
  (add-hook 'minibuffer-setup-hook (lambda ()
                                     ;; Do not allow the cursor in the minibuffer prompt
                                     (cursor-intangible-mode 1)))
#+end_src

** minibuffer-mode hooks
These hooks run after =minibuffer-mode= activates for a buffer.

#+begin_src emacs-lisp
  (add-hook 'minibuffer-mode-hook (lambda ()
                                    ;; Don't highlight whitespace in minibuffer.
                                    (setq-local show-trailing-whitespace nil)
                                    (whitespace-mode -1)))
#+end_src

** prog-mode hooks
These commands run whenever Emacs finds a file of any programming language.

#+begin_src emacs-lisp
  (add-hook 'prog-mode-hook (lambda ()
                              ;; Make hyperlinks clickable.
                              (goto-address-mode 1)
                              ;; Turn various keywords into pretty programming
                              ;; symbols, such as "lambda" -> "λ" in lisp-mode.
                              (prettify-symbols-mode 1)
                              ;; Show invisible characters.
                              (whitespace-mode 1)))
#+end_src

** shell-mode hooks
shell-mode is a basic terminal emulator in Emacs.

#+begin_src emacs-lisp
  (add-hook 'shell-mode-hook (lambda ()
                               (ansi-color-for-comint-mode-on)))
#+end_src

** text-mode hooks
These commands run whenever Emacs finds a text type file or any of its
derivatives.

#+begin_src emacs-lisp
  (add-hook 'text-mode-hook (lambda ()
                              ;; Wrap words if they exceed the fill column
                              ;; threshold.
                              (auto-fill-mode 1)
                              ;; Make hyperlinks clickable.
                              (goto-address-mode 1)
                              ;; Show invisible characters.
                              (whitespace-mode 1)))
#+end_src

** conf-mode hooks
These commands run whenever Emacs finds a configuration file, such as =.ini=
or =.gitconfig= files.

#+begin_src emacs-lisp
  (add-hook 'conf-mode-hook (lambda ()
                              ;; Make hyperlinks clickable.
                              (goto-address-mode 1)
                              ;; Show invisible characters.
                              (whitespace-mode 1)))
#+end_src

** before-save hooks
These hooks run before Emacs saves a file.

#+begin_src emacs-lisp
  (add-hook 'before-save-hook (lambda ()
                                ;; Strip trailing whitespace from the
                                ;; current buffer before saving.
                                (delete-trailing-whitespace)
                                ;; Convert tabs to spaces.
                                (untabify (point-min) (point-max))))
#+end_src

** after-save hooks
These hooks run after Emacs saves a file.

#+begin_src emacs-lisp
  (add-hook 'after-save-hook
            (lambda ()
              ;; Update any GTAGS files if necessary.
              (my-gtags-update-hook-fn)))
#+end_src

* Load Private Configuration
There are times when we need to write environment-specific configuration
containing sensitive information, such as usernames and passwords. My solution
is to create an untracked directory =~/.emacs.d/private/= containing all of the
Emacs configuration I want to keep private to my current machine, and load that
configuration here if it is present.

#+begin_src emacs-lisp
  (when (file-directory-p my/private-elisp-dir)
    (my/apply-to-dir-files my/private-elisp-dir "\\.el$"
                           (lambda (f &rest args)
                             "Make use of `load''s extensionless file loading
      feature for Elisp files. This means `load' will first look for an .elc file,
      then for a .el file in lieu of that."
                             (load (file-name-sans-extension f) args))))
#+end_src

* Periodically Purge Backup/Temp Files
We do not want to clutter up our backup and auto-save file directories with old,
stale files. We should periodically purge old files from these directories.

#+begin_src emacs-lisp
  (message "Deleting old backup and auto-save files...")
  (let ((week (* 60 60 24 7))
        (current (float-time (current-time))))
    (dolist (file (append (directory-files
                           (concat (file-truename my/emacs-backup-dir) "/") t)
                          (directory-files
                           (concat (file-truename my/emacs-auto-saves-dir) "/") t)))
      (when (and (backup-file-name-p file)
                 (> (- current (float-time (nth 5 (file-attributes file))))
                    week))
        (message "%s" file)
        (delete-file file))))
#+end_src

* Install Packages Needed on Startup
Some packages are important to load right at the get-go, either because we
want their functionality right now, or because they provide extra keywords
for =use-package= that we want to make use of in our =use-package=
statements.

** exec-path-from-shell
Keep Emacs' own =PATH= environment variable in sync with the user's =PATH=,
making sure that all external executable available to the user are also within
Emacs' reach.

*NOTE:* Running =exec-path-from-shell= can be slow since it has to spawn an
external shell process and parse the =PATH= environment variable from that
process. I prefer to parse my =PATH= from the =~/.path_static= file, since that
path is already calculated.

#+begin_src emacs-lisp
  (if (and (member system-type '(gnu gnu/linux darwin cygwin))
           (file-exists-p my/static-path-file))
      ;; If we have compiled our `~/.path_static` file, use the PATH in that file
      ;; because using that PATH is much faster than calculating it dynmaically.
      (my/load-system-path)
    (use-package exec-path-from-shell
      :demand t
      :init
      ;; Whether to output debug info to the *Messages* buffer.
      ;; NOTE: This variable is not customizable.
      (setq exec-path-from-shell-debug nil)
      :custom
      ;; How long to wait before warning about long startup time for shell.
      (exec-path-from-shell-warn-duration-millis 500)
      :config
      ;; Only run this plugin for macOS, Linux, or Cygwin systems.
      (when (member system-type '(gnu gnu/linux darwin cygwin))
        ;; Make sure to use the lean version of our login shell profile to
        ;; avoid timing out or excessive memory consumption.
        (setenv "USE_LEAN_PROFILE" "true")
        ;; Set $PATH by running the user's login shell.
        (exec-path-from-shell-initialize)
        ;; Remove USE_LEAN_PROFILE environment variable once it has served its
        ;; purpose.
        (setenv "USE_LEAN_PROFILE" nil))))
#+end_src

** delight
=delight.el= allows users to remove or alter the lighter text for both major and
minor modes in the Emacs mode line. Users may call =delight= directly with
=use-package= by providing the =:delight= keyword to the =use-package= macro.

*** Usage
=delight.el= is easy to use and flexible, providing a single entrypoint into its
API: the =delight= function, which takes one to three arguments.

The first argument is a symbol representing the major or minor mode whose mode
line test we would like to alter.

The second argument is the replacement lighter text, or =nil= to remove the
lighter altogether.

The third argument will change depending on whether you are modifying a major or
minor more. If you want to alter a major mode's lighter, the third argument is
always the keyword =:major=. If you want to alter a minor mode's lighter, the
third argument is a symbol representing the name of the feature that provides
the minor mode.

For example:

#+begin_src emacs-lisp :tangle no
  (require 'delight)
  (delight 'abbrev-mode " Abv" 'abbrev)
  (delight 'rainbow-mode)
#+end_src

The =delight= function also allows modifying the lighter text for multiple modes
in a single function call. In this case, the sole argument to =delight= is a
list of argument lists, each one representing a single call to =delight= as
detailed above:

#+begin_src emacs-lisp :tangle no
  (require 'delight)
  (delight '((abbrev-mode " Abv" abbrev)
             (smart-tab-mode " \\t" smart-tab)
             (eldoc-mode nil eldoc)
             (rainbow-mode)
             (overwrite-mode " Ov" t)
             (emacs-lisp-mode "Elisp" :major)))
#+end_src

*** Integration with =use-package=
=delight= provides the =:delight= keyword for the =use-package= macro. If you
use =use-package= as your package configuration system, this is /by far/ the
best way to use =delight=. The following comes from the =use-package= README:

#+begin_quote
=delight= is invoked with the =:delight= keyword, which is passed a minor mode
symbol, a replacement string or quoted mode-line data (in which case the minor
mode symbol is guessed to be the package name with "-mode" appended at the end),
both of these, or several lists of both. If no arguments are provided, the
default mode name is hidden completely.
#+end_quote

*** =use-package= specification
#+begin_src emacs-lisp
  (use-package delight
    :demand t
    :config
    ;; Remove lighter text for whitespace-mode
    (delight '((whitespace-mode nil whitespace)
               (auto-fill-function nil simple)
               (subword-mode nil subword)
               (visual-line-mode nil simple))))
#+end_src

** key-chord
This package maps pairs of simultaneously pressed keys to bindings.

#+begin_src emacs-lisp
  (use-package key-chord
    :demand t
    :config
    (key-chord-mode 1))
#+end_src

** general
=general.el= is a macro system for managing Emacs keybindings. You can think
of it as a wrapper around both standard Emacs keybinding forms, such as
=define-key=, =global-set-key=, etc., and third-party keybinding macros,
such as =evil-define-key=. Thus, General allows you to define keys in a
package-agnostic fashion. General also adds a number of keywords to
=use-package=, such as =:general=, =ghook=, and =gfhook= for defining keys
and hooks in a way that defers loading the package.

#+begin_src emacs-lisp
  (use-package general
    :demand t
    :preface
    (require 'general)
    :config
    (add-to-list 'load-path (file-truename (concat user-emacs-directory "straight/build/general")))
    (require 'general)
    ;; Define a shortcut function for defining keys that begin with "C-c"
    (general-create-definer my/user-leader-def
      :prefix "C-c")
    ;; Create a replacement macro for `evil-leader'. This obsoletes the need to
    ;; include `evil-leader' in your configuation!
    (general-create-definer my/evil-leader-def
      :states '(normal visual)
      :prefix ",")
    ;; Allows using Vim-style key definers.
    ;; Available definers are:
    ;;   general-imap
    ;;   general-emap
    ;;   general-nmap
    ;;   general-vmap
    ;;   general-omap
    ;;   general-mmap
    ;;   general-rmap
    ;;   general-iemap
    ;;   general-nvmap
    ;;   general-otomap
    ;;   general-itomap
    ;;   general-tomap
    ;; If you pass a non-nil argument to `general-evil-setup', you may omit the "general-" prefix for
    ;; these macros. I find that leaving them in makes your code more descriptive, however.
    (general-evil-setup)
  ;;; Set universal keybindings with General
    (general-unbind
      ;; Unbind some keys to make room for my custom keybindings.
      "M-c")
    (general-def
      ;; Nobody uses downcase-region, anyway.
      "C-x C-l" 'find-library
      ;; Compile command for the current buffer.
      "M-c c" 'compile
      ;; Re-run the previous compile command.
      "M-c r" 'recompile
      ;; Kill the running compilation process.
      "M-c k" 'kill-compilation
      ;; Much easier than `C-x 4 C-f'
      "C-x F" 'find-file-other-window
      ;; Kill the current buffer.
      "C-M-k" 'kill-this-buffer
      ;; Duplicate the current line or region N times.
      "C-S-p" 'duplicate-dwim)
    ;; "C-c" keybindings:
    (my/user-leader-def 'visual
      ;; Use `C-c (' to align a visual region.
      "(" 'align)
    ;; Evil leader shortcuts:
    (my/evil-leader-def
      ;; Move cursor to beginning/end of line.
      "m" 'evil-first-non-blank
      "." 'evil-end-of-line
      ;; Evaluating s-exps
      "<" 'eval-last-sexp
      ">" 'eval-print-last-sexp
      ;; Toggle relative/absolute line numbers.
      "N"  'my/toggle-line-number-type
      ;; Window
      "ad" 'delete-window
      "af" 'delete-other-windows
      "as" 'split-window-right
      "aw" 'split-window-below
      ;; Bookmarks
      "bd" 'bookmark-delete
      "bj" 'bookmark-jump
      "bl" 'list-bookmarks
      "bs" 'bookmark-set
      ;; Buffer
      "bb" 'switch-to-buffer
      "bd" 'evil-delete-buffer
      "bk" 'kill-this-buffer
      "kk" 'kill-buffer
      ;; Dired
      "dd" 'dired
      "dw" 'dired-other-window
      ;; eshell
      "es" 'eshell-below
      ;; File
      "ff" 'find-file-at-point
      "fF" 'find-file-other-window
      "fr" 'recentf-open-files
      "lf" 'load-file))
#+end_src

** org
We should load =org= now to avoid version mismatch errors that could occur if we
try to load =org= later.

#+begin_src emacs-lisp
  (use-package org
    ;; Use built-in Org version to avoid version compatibility issues.
    ;; You may want to comment the `:ensure' and `:straight' directives
    ;; from time to time in order to upgrade your Org version.
    ;; :ensure nil
    ;; :straight (org :type built-in)
    :delight
    (org-src-mode nil org-src)
    (org-indent-mode nil org-indent)
    :general
    (general-def org-mode-map
      ;; Mainly used to cycle through todo states.
      "C-M-l" 'org-shiftright
      "C-M-h" 'org-shiftleft)
    (general-def 'insert org-mode-map
      ;; Cycle/continue to next option depending on context.  This moves through
      ;; fields in a table, opens/closes org headings, etc.
      "C-l"   'org-cycle
      ;; Bind usual Tab key behavior (indent or complete depending on point position).
      "TAB"   'indent-for-tab-command
      "<tab>" 'indent-for-tab-command)
    (my/evil-leader-def org-mode-map
      "cc" 'org-edit-special
      "oe" 'org-export-dispatch
      ;; Export current Org buffer to PDF
      "oE" 'org-latex-export-to-pdf
      "ol" 'org-babel-load-file
      "op" 'org-latex-preview)
    (my/user-leader-def
      ;; These bindings should be available everywhere.  We'll want to use them
      ;; outside of org-mode.
      "M-o l" 'org-store-link
      "M-o a" 'org-agenda)
    (my/evil-leader-def
      "oa" 'org-agenda)
    ;; Leader shortcuts available when special-editing source blocks.
    (my/evil-leader-def org-src-mode-map
      "cc" 'org-edit-src-exit
      "ck" 'org-edit-src-abort)
    :custom
    ;; Do not show all "*" characters for each heading, but instead show only the final "*".
    (org-hide-leading-stars t)
    ;; Whether to only show headings when visiting a new Org file.
    (org-startup-folded 'fold)
    ;; Whether to open Org mode buffers with `org-indent-mode' enabled.
    (org-startup-indented t)
    ;; Which LaTeX compiler command to use. Defaults to "pdflatex".
    (org-latex-compiler (cond
                         ((executable-find "xelatex") "xelatex")
                         (t "pdflatex")))
    ;; The CLI program used to convert LaTeX fragments to image files.
    ;; default value: 'dvipng
    (org-latex-create-formula-image-program 'imagemagick)
    ;; Whether to use `lstlisting' for source environments.
    (org-latex-listings t)
    ;; Backend used to generate source code listings.
    ;; Can be one of (ordered from least to most capable):
    ;;   'verbatim
    ;;   'listings
    ;;   'minted (requires the `pygments' Python package)
    ;;   'engraved (requires the `engrave-faces' Emacs package and LaTeX `fvextra' package)
    (org-latex-src-block-backend 'engraved)
    ;; Whether to show LaTeX previews when opening a new Org buffer.
    (org-startup-with-latex-preview t)
    ;; The default LaTeX preview program to use.
    ;; Can be one of the following:
    ;; (you must have the corresponding CLI program installed)
    ;;   'dvipng (default)
    ;;   'dvisvgm
    ;;   'imagemagick (uses the `convert' program)
    (org-preview-latex-default-process (cond
                                        ((and (executable-find "convert")
                                              (string-match-p "\sIMAGEMAGICK\s"
                                                              system-configuration-features)
                                              'imagemagick))
                                        ((executable-find "dvisvgm")
                                         'dvisvgm)
                                        (t
                                         'dvipng)))
    ;; Directory used by Org only in rare circumstances, such as when filing
    ;; away remember notes.
    (org-directory my/org-dir)
    ;; Pressing return while point is over a hyperlink will open the link in
    ;; the user's web browser.
    (org-return-follows-link t)
    ;; Record a timestamp when a todo item is marked as done.
    (org-log-done 'time)
    ;; The keywords to use when cycling through org-todo. In the parentheses, the
    ;; letter is a key you press to immediately transition a todo to the
    ;; appropriate state. The `@' character means we should capture a note when
    ;; entering that state.
    (org-todo-keywords '((sequence "TODO(t)"
                                   "NEXT(n)"
                                   "WAITING(w@)"
                                   "INACTIVE(i@)"
                                   "MEETING(m)"
                                   "|"
                                   "DONE(d)"
                                   "CANCELLED(c@)")))
    ;; Set Org agenda files to a list of files and/or directories.
    (org-agenda-files `(,my/org-agenda-dir))
    ;; Whether to prompt the user for confirmation before evaluating source
    ;; blocks.
    (org-confirm-babel-evaluate nil)
    ;; Whether to show images when opening an Org file.
    (org-startup-with-inline-images t)
    ;; Whether to keep images their original size or to shrink them to fit their
    ;; corresponding Org mode buffer.
    (org-image-actual-width nil)
    ;; PlantUML configuration
    ;; Whether to use the PlantUML JAR file or the executable.
    ;;   'plantuml -> use executable
    ;;   'jar -> use JAR file
    (org-plantuml-exec-mode 'plantuml)
    (org-plantuml-executable-path (executable-find "plantuml"))
    :init
    ;; Remove a few unnecessary packages from Org's default LaTeX package list.
    ;; (dolist (pkg '(("T1" "fontenc" t)
    ;;                ("AUTO" "inputenc" t ("pdflatex"))))
    ;;   (setq org-latex-default-packages-alist (remove pkg org-latex-default-packages-alist)))
    (add-hook 'org-mode-hook
              (lambda ()
                ;; Disable whitespace mode for org mode.
                (whitespace-mode -1)
                (setq-local show-trailing-whitespace nil)
                ;; Org mode requires a `tab-width' value of 8, for some reason.
                (setq-local tab-width 8)))
    (add-hook 'org-src-mode-hook
              (lambda ()
                ;; Fix Evil mode keybindings in Org's special-edit buffers (Source code buffers
                ;; entered by pressing `C-c \'').
                (evil-normalize-keymaps)))
    ;; Show images after evaluating code blocks.
    (add-hook 'org-babel-after-execute-hook #'org-display-inline-images)
    ;; Make sure the Org notes directory is present.
    (make-directory my/org-dir t)
    ;; Also create the agenda directory.
    (make-directory my/org-agenda-dir t)
    :config
    ;; Use `lualatex' to create DVI/PDF output for LaTeX previews. Also, add '--shell-escape' flag to
    ;; all LaTeX commands that Org uses to generate previews, and make sure that `:latex-compiler'
    ;; command strings generate DVI files when appropriate.
    (let ((dvipng-config (alist-get 'dvipng org-preview-latex-process-alist))
          (dvisvgm-config (alist-get 'dvisvgm org-preview-latex-process-alist))
          (imagemagick-config (alist-get 'imagemagick org-preview-latex-process-alist)))
      (setf dvipng-config (plist-put dvipng-config
                                     :latex-compiler `(,(concat "lualatex"
                                                                " --shell-escape"
                                                                " --interaction=nonstopmode"
                                                                " --output-format=dvi"
                                                                " --output-directory=%o"
                                                                " %f"))))
      (setf dvisvgm-config (plist-put dvisvgm-config
                                      :latex-compiler `(,(concat "lualatex"
                                                                 " --shell-escape"
                                                                 " --interaction=nonstopmode"
                                                                 " --output-format=dvi"
                                                                 " --output-directory=%o"
                                                                 " %f"))))
      (setf imagemagick-config (plist-put imagemagick-config
                                          :latex-compiler `(,(concat "lualatex"
                                                                     " --shell-escape"
                                                                     " --interaction=nonstopmode"
                                                                     " --output-format=pdf"
                                                                     " --output-directory=%o"
                                                                     " %f")))))
    ;; Required for expand-region.
    (require 'org-fold)
    ;; Add more default LaTeX packages
    (dolist (package '(("" "amsthm" t)
                       ("" "color" t)
                       ("" "enumitem" t)
                       ("" "fontspec" t)
                       ("letterpaper,margin=1in" "geometry")
                       ("" "grffile" t)
                       ("" "listings" t)
                       ("" "minted" t)
                       ("" "parskip" t)
                       ("" "pgfplots" t)
                       ("" "sectsty" t)
                       ("" "setspace" t)
                       ("" "svg" t)
                       ("" "textcomp" t)
                       ("" "tikz" t)
                       ("dvipsnames" "xcolor" t)))
      (add-to-list 'org-latex-default-packages-alist package))
    ;; The command used to generate PDFs from Org's LaTeX exports.
    (setq org-latex-pdf-process (list (concat "latexmk -f -%latex"
                                              " -%latex='%latex -shell-escape %%O %%S'"
                                              " -recorder -synctex=1 -bibtex-cond"
                                              " -interaction=nonstopmode -output-directory=%o %f")))
    ;; Register PlantUML as an Org-compatible language for source blocks.
    (add-to-list 'org-src-lang-modes '("plantuml" . plantuml))
    (org-babel-do-load-languages 'org-babel-load-languages
                                 '((emacs-lisp . t)
                                   (gnuplot . t)
                                   (js . t)
                                   (latex . t)
                                   (org . t)
                                   (perl . t)
                                   (plantuml . t)
                                   (python . t)
                                   (R . t)
                                   (shell . t))))
#+end_src

** org-auto-tangle
=org-auto-tangle= is a simple emacs package that allows you to automatically
tangle org files on save. You do this by adding the option =#+auto_tangle: t= in
your org file.

The tangling process happens asynchronously so it will not block your emacs
session.

If =org-auto-tangle-mode= is on, it will try to automatically tangle your org
files if they contain a non-nil value for the =#+auto_tangle:= option.

#+begin_src emacs-lisp
  (use-package org-auto-tangle
    :after org
    :delight
    :hook
    (org-mode . org-auto-tangle-mode)
    :custom
    ;; Whether to use auto-tangle as the default behavior for all org buffers.
    (org-auto-tangle-default t))
#+end_src

** ediff
=ediff= is Emacs' built-in file diff tool. Although originally designed for
viewing generic file diffs in ye olden times, =ediff= works wonderfully for
viewing Git diffs between commits, merging diffs during Git merge conflicts, and
much more.

#+begin_src emacs-lisp
  (use-package ediff
    :custom
    ;; `nil' means prompt to remove unmodified buffers A/B/C at session end.
    (ediff-keep-variants nil)
    ;; Whether to make all variant buffers read-only when `ediff' stars.
    (ediff-make-buffers-readonly-at-startup nil)
    ;; (ediff-merge-revisions-with-ancestor t)
    ;; If `t', show only diff regions where both buffers disagree with the ancestor.
    (ediff-show-clashes-only t)
    ;; The function used to split the main window between buffer-A and buffer-B.
    (ediff-split-window-function #'split-window-horizontally)
    ;; Function called to set up windows.
    (ediff-window-setup-function #'ediff-setup-windows-plain))
#+end_src

** tree-sitter
Emacs 29 introduced support for =tree-sitter=, a powerful language parsing
library that greatly enhances Emacs' semantic understanding of source code.

#+begin_src emacs-lisp
  (require 'straight)
  (require 'treesit)

  (use-package treesit
    ;; `treesit' is a built-in feature, and does not exist in ELPA/MELPA.
    :ensure nil
    :straight nil
    :demand t
    :if (and (fboundp 'treesit-available-p)
             (treesit-available-p))
    :preface
    ;; Make sure the Treesitter grammar alist is defined.
    (unless (boundp 'treesit-language-source-alist)
      (setq treesit-language-source-alist nil))
    ;; Specifies configuration for downloading and installing `tree-sitter' language grammars.
    (dolist (grammar
             '((bash       "https://github.com/tree-sitter/tree-sitter-bash")
               (c          "https://github.com/tree-sitter/tree-sitter-c")
               (cpp        "https://github.com/tree-sitter/tree-sitter-cpp")
               (clojure    "https://github.com/sogaiu/tree-sitter-clojure")
               (cmake      "https://github.com/uyha/tree-sitter-cmake")
               (commonlisp "https://github.com/theHamsta/tree-sitter-commonlisp")
               (css        "https://github.com/tree-sitter/tree-sitter-css")
               (dockerfile "https://github.com/camdencheek/tree-sitter-dockerfile")
               (elisp      "https://github.com/Wilfred/tree-sitter-elisp")
               (elixir     "https://github.com/elixir-lang/tree-sitter-elixir")
               (go         "https://github.com/tree-sitter/tree-sitter-go")
               (haskell    "https://github.com/tree-sitter/tree-sitter-haskell")
               (heex       "https://github.com/phoenixframework/tree-sitter-heex")
               (html       "https://github.com/tree-sitter/tree-sitter-html")
               (java       "https://github.com/tree-sitter/tree-sitter-java")
               (javascript "https://github.com/tree-sitter/tree-sitter-javascript" "master" "src")
               (json       "https://github.com/tree-sitter/tree-sitter-json")
               (latex      "https://github.com/latex-lsp/tree-sitter-latex")
               (make       "https://github.com/alemuller/tree-sitter-make")
               (markdown   "https://github.com/ikatyang/tree-sitter-markdown")
               (org        "https://github.com/milisims/tree-sitter-org")
               (python     "https://github.com/tree-sitter/tree-sitter-python")
               (toml       "https://github.com/tree-sitter/tree-sitter-toml")
               (tsx        "https://github.com/tree-sitter/tree-sitter-typescript" "master" "tsx/src")
               (typescript "https://github.com/tree-sitter/tree-sitter-typescript" "master"
                           "typescript/src")
               (yaml       "https://github.com/ikatyang/tree-sitter-yaml")))
      (add-to-list 'treesit-language-source-alist grammar))
    ;; Here we remap major modes to ensure that the correct `tree-sitter' enabled mode activates,
    ;; instead.
    (dolist (mapping
             '((c-mode          . c-ts-mode)
               (c++-mode        . c++-ts-mode)
               (cmake-mode      . cmake-ts-mode)
               (csharp-mode     . csharp-ts-mode)
               (css-mode        . css-ts-mode)
               (dockerfile-mode . dockerfile-ts-mode)
               (elixir-mode     . elixir-ts-mode)
               (go-mode         . go-ts-mode)
               (go-dot-mod-mode . go-mod-ts-mode)
               (html-mode       . html-ts-mode)
               (java-mode       . java-ts-mode)
               (js-mode         . js-ts-mode)
               (json-mode       . json-ts-mode)
               (lua-mode        . lua-ts-mode)
               (python-mode     . python-ts-mode)
               (rjsx-mode       . tsx-ts-mode)
               (ruby-mode       . ruby-ts-mode)
               (rust-mode       . rust-ts-mode)
               (sh-mode         . bash-ts-mode)
               (toml-mode       . toml-ts-mode)
               (typescript-mode . typescript-ts-mode)
               (yaml-mode       . yaml-ts-mode)))
      (add-to-list 'major-mode-remap-alist mapping))
    (defun my/install-treesit-grammars (&optional force-reinstall)
      "Install any missing tree-sitter grammars to `~/.emacs.d/tree-sitter/'.

  If FORCE-REINSTALL is non-nil, reinstall all tree-sitter grammars."
      (interactive "P")
      (let ((ts-grammars-dir (file-truename (concat user-emacs-directory "tree-sitter")))
            (ts-grammars (mapcar #'car treesit-language-source-alist)))
        (if (file-directory-p ts-grammars-dir)
            ;; If the tree-sitter grammars dir exists, check its contents and only install missing
            ;; grammars.
            (dolist (g ts-grammars)
              (when (or force-reinstall (not (treesit-language-available-p g)))
                (treesit-install-language-grammar g ts-grammars-dir)))
          ;; If the tree-sitter grammar dir does not exist, install all grammars from scratch.
          (mapc (lambda (g) (treesit-install-language-grammar g ts-grammars-dir)) ts-grammars))))
    :config
    (my/install-treesit-grammars))
#+end_src

** xref
=xref= comes with Emacs 28.1 and newer, but sometimes we don't get access to the
latest and greatest, so we can also download it from ELPA.

#+begin_src emacs-lisp
  (use-package xref
    :demand t
    :custom
    (xref-show-definitions-function #'xref-show-definitions-completing-read)
    ;; The program to use to find xref symbols.
    (xref-search-program (cond ((executable-find "rg") 'ripgrep)
                               ((executable-find "ugrep") 'ugrep)
                               (t "grep"))))
#+end_src
